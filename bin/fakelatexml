#!/usr/bin/perl -w
use strict;
use warnings;
use FindBin;
use lib "$FindBin::RealBin/../lib";
use Getopt::Long qw(:config no_ignore_case);
use Pod::Usage;
use YAML::Tiny;
use Data::Dumper;
use Text::Wrap;

my $CONFIGDIR = "$FindBin::RealBin/../lib/Config";
#======================================================================
# Command line options translation
#======================================================================
# Parse command line
my $verbosity;
my $splitback  = "//ltx:bibliography | //ltx:appendix | //ltx:index";
my %splitpaths = (
  part       => "//ltx:part | $splitback",
  chapter    => "//ltx:part | //ltx:chapter | $splitback",
  section    => "//ltx:part | //ltx:chapter | //ltx:section | $splitback",
  subsection => "//ltx:part | //ltx:chapter | //ltx:section | //ltx:subsection | $splitback",
  subsubsection => "//ltx:part | //ltx:chapter | //ltx:section | //ltx:subsection | //ltx:subsubsection | $splitback");

my $sawformat;
my $sawinputformat;
my $destination;
my $opts = {};    # not-yet-handled options.
# NOTE: Need provision for checking for allowed values of certain parameters?
#  Handled during command-line processing? during config parsing? during instanciation?
my @options = ();
sub add { push(@options, @_); }
sub Boole { return ($_[0] ? 'true' : 'false'); }
my $spec = {
  "output=s"      => sub { add("global:\n  destination: '$_[1]'"); $destination = $_[1]; },
  "destination=s" => sub { add("global:\n  destination: '$_[1]'"); $destination = $_[1]; },
  "base=s"        => sub { add("global:\n  basedir: '$_[1]'"); },                 # diff from sitedir?
  "log=s"         => sub { add("global:\n  log: '$_[1]'"); },
  "path=s"        => sub { add("global:\n  paths:\n  -'$_[1]'"); },
  "strict"        => sub { add("global:\n  strict: true"); },
  "validate!"     => sub { add("global:\n   validate: " . Boole($_[1])); },
  "preload=s"     => sub { add("digest:\n  preload: '$_[1]'"); },
  "preamble=s"    => sub { add("digest:\n  preamble: '$_[1]'"); },
  "postamble=s"   => sub { add("digest:\n  postamble: '$_[1]'"); },
  "comments!"     => sub { add("digest:\n  includecomments: " . Boole($_[1])); },
  "documentid=s"  => sub { add("digest:\n  documentid: '$_[1]'"); },
  "includestyles" => sub { add("digest:\n  includestyles: true"); },
  "inputencoding=s" => sub { add("digest:\n  inputencoding : '$_[1]'"); },
  "xml"             => sub { add("include: xml"); $sawformat = 1; },
  "tex"             => sub { add("include: tex"); $sawformat = 1; },         # TeX OUTPUT!!!!
  "box"             => sub { add("include: box"); $sawformat = 1; },
  "format=s"        => sub { add("include: $_[1]"); $sawformat = 1; },
  "bibtex"          => sub { add("include: bib"); $sawinputformat = 1; },
  "profile=s"       => sub { add("include: '$_[1]'"); },
  "mode=s"          => sub { add("include: '$_[1]'"); },                     # alias
  "noparse"         => sub { add("mathparser:\n  enabled: false"); },
  "parse=s" => sub { add("mathparser:\n  enabled: true\n  engine: '$_[1]'"); },
  "cache_key=s"              => \$$opts{cache_key},                                         # ?
  "source=s"                 => \$$opts{source},                                            # ?
  "embed"                    => sub { add("packager:\n whatsout: fragment"); },
  "whatsin=s"                => sub { add("global:\n  whatsin: '$_[1]'"); },                # global??
  "whatsout=s"               => sub { add("packager:\n whatsout: '$_[1]'"); },
  "autoflush=i"              => sub { add("daemon:\n   input_limit: '$_[1]'"); },
  "timeout=i"                => sub { add("daemon:\n   timeout: '$_[1]'"); },
  "expire=i"                 => sub { add("daemon:\n   expire: '$_[1]'"); },
  "address=s"                => sub { add("daemon:\n   address: '$_[1]'"); },
  "port=i"                   => sub { add("daemon:\n   port: '$_[1]'"); },
  "post!"                    => sub { add("include: post"); },
  "omitdoctype!"             => sub { add("writer:\n   omitdoctype: " . Boole($_[1])); },
  "mathimages!"              => sub { add("mathimages:\n   enabled: " . Boole($_[1])); },
  "mathimagemagnification=f" => sub { add("mathimages:\n   magnification: '$_[1]'"); },
  "mathsvg!"                 => sub { add("mathsvg:\n   enabled: " . Boole($_[1])); },
  "linelength=i"             => sub { add("pmml:\n   linelength: $_[1]"); },
  "plane1!"                  => sub { add("pmml:\n   plane1: " . Boole($_[1])); },
  "hackplane1!"              => sub { add("pmml:\n   hackplane1: " . Boole($_[1])); },
  "presentationmathml|pmml!" => sub { add("pmml:\n   enabled: " . Boole($_[1])); },
  "contentmathml|cmml!"      => sub { add("cmml:\n   enabled: " . Boole($_[1])); },
  "openmath|om!"             => sub { add("om:\n   enabled: " . Boole($_[1])); },
  "keepXMath!"               => sub { add("xmath:\n   enabled: " . Boole($_[1])); },
  "mathtex!"                 => sub { add("mathtex:\n   enabled: " . Boole($_[1])); },
  "stylesheet=s" => sub { add("xslt:\n  enabled: true\n  stylesheet: '$_[1]'"); },
  "xsltparameter=s"   => sub { add("xslt:\n  enabled: true\n  parameters:\n - '$_[1]'"); },
  "timestamp=s"       => sub { add("xslt:\n  timestamp: '$_[1]'"); },
  "defaultresources!" => sub { add("xslt:\n  defaultresources: " . Boole($_[1])); },
  "css=s"             => sub { add("xslt:\n  css:\n  - '$_[1]'"); },
  "javascript=s"      => sub { add("xslt:\n  javascript:\n  - '$_[1]'"); },
  "icon=s"            => sub { add("xslt:\n  icon: '$_[1]'"); },
  "split!"            => sub { add("split:\n  enabled: " . Boole($_[1])); },
  "splitat=s"         => sub { my $path = $splitpaths{ $_[1] };
    configWarning("Value not in [" . join(',', keys %splitpaths) . "]",
      $_[1], 'split', 'splitat') unless $path;
    add("split:\n  enabled: true\n  splitpath: '$path'"); },
  "splitpath=s"   => sub { add("split:\n  enabled: true\n  splitpath: '$_[1]'"); },
  "splitnaming=s" => sub { add("split:\n  enabled: true\n  splitnaming: '$_[1]'"); },
  "scan!"         => sub { add("scan:\n  enabled: true"); },
  "prescan"    => sub { add("scan:\n  enabled: true"); },           # AND disable other post!!!
  "crossref!"  => sub { add("crossref:\n  enabled: true"); },
  "urlstyle=s" => sub { add("crossref:\n  urlstyle: '$_[1]'"); },
  "navigationtoc|navtoc=s" => sub { add("crossref { navigation_toc: '$_[1]'"); },
  "numbersections!"        => sub { add("crossref:\n   numbersections: " . Boole($_[1])); },
  "index!"                 => sub { add("index:\n   enabled: " . Boole($_[1])); },
  "permutedindex!"         => sub { add("index:\n   permuted: true"); },
  "splitindex!"            => sub { add("index:\n   split: " . Boole($_[1])); },
  "bibliography=s"         => sub { add("bib:\n   bibliographies:\n  - '$_[1]'"); },
  "nobibliography"         => sub { add("bib:\n   enabled: false"); },
  "splitbibliography!"     => sub { add("bib:\n   split: " . Boole($_[1])); },
  "dbfile=s"               => sub { add("global:\n   dbfile: '$_[1]'"); },
  "sitedirectory=s"        => sub { add("global:\n   sitedirectory: '$_[1]'"); },
  "resourcedirectory=s"    => sub { add("global:\n   resourcedirectory: '$_[1]'"); },
  "sourcedirectory=s"      => sub { add("global:\n   sourcedir: '$_[1]'\n"); },
  "graphicimages!"         => sub { add("graphics:\n   enabled: " . Boole($_[1])); },
  "graphicsmap=s" => sub {    # graphicsmap=srctype.desttype
    my ($src, $dest) = split(/\./, $_[1]);
    $dest = $src unless $dest;
    add("graphics:\n  graphics_types: $src\n"
        . "  type_properties:\n    $src:\n      destination_type: $dest"); },
  "svg!"           => sub { add("picturesvg:\n   enabled: " . Boole($_[1])); },
  "pictureimages!" => sub { add("pictureimages:\n   enabled: " . Boole($_[1])); },
  # HELP

  "quiet"   => sub { $verbosity--; },
  "verbose" => sub { $verbosity++; },

  "VERSION!" => \$$opts{showversion},
  "debug=s"  => \@{ $$opts{debug} },
  "help"     => \$$opts{help}
};

#======================================================================
GetOptions(%$spec);
my $source = $ARGV[0];

# Add a couple of special case options
if (!$sawformat && $destination && ($destination =~ /\.(\w+)$/)) {
  unshift(@options, "include: $1"); }
# Load configuration implied by input & output formats
if (!$sawinputformat && $source && ($source =~ /\.(tex|bib)$/)) {
  unshift(@options, "include: $1"); }
unshift(@options, "global:\n  verbosity: $verbosity") if $verbosity;

#======================================================================
my $yoptions = join("\n---\n", @options) . "\n";
print "Options as YAML:\n$yoptions\n";

our $config = LoadConfiguration($yoptions);

#print "Config: " . Dumper($config);
makePlan($config);

#======================================================================
# The known Processing Phases
our @PHASES;

BEGIN {
  # Try to condense this a bit more? (using sensible "position" values to sort within phases)
  # And, not yet configurable either!!
  @PHASES = (qw(
      digestion
      splitting
      scanning
      crossreferencing
      graphicstranslation
      mathtranslation
      xslt
      packaging));
}
#======================================================================
sub LoadConfiguration {
  my ($options) = @_;    # options in YAML form
  my $config = {};
  # Load base configuration
  loadConfigFile($config, 'LaTeXML');
  # Could load a user-configuration file?

  # Now invoke the configuration (multiple yaml docs) implied by command-line options
  mergeConfig($config, YAML::Tiny->read_string($options));
  return $config; }

sub loadConfigFile {
  my ($config, $source) = @_;
  local $LaTeXML::CONFIG_FILE = $source;
  # Eventually search for config files
  my $path = $CONFIGDIR . '/' . $source . '.ltxcfg';
  if (-f $path) {
    print "Loading configuration $source ($path)\n";
    mergeConfig($config, YAML::Tiny->read($path)); }
  else {
    warn "Missing config file $path\n"; } }

# Merge the $yaml data into $config
sub mergeConfig {
  my ($config, $yaml) = @_;
  if (!$yaml) {
    warn "Yaml error: " . YAML::Tiny->errstr . "\n"; }
  foreach my $ydoc (@$yaml) {    # foreach yaml "document"
    foreach my $name (keys %$ydoc) {
      my $value = $$ydoc{$name};
      if ($name eq 'include') {
        loadConfigFile($config, $value); }
      else {
        mergeValue($config, $value, $name); } } } }

sub mergeValue {
  my ($config, $value, @keys) = @_;
  my $object = configLookup($config, @keys[0 .. $#keys - 1]);
  my $key    = $keys[-1];
  my $prev   = $$object{$key};
  my $struct = 'scalar';    # the type of structure to maintain in $config: HASH, ARRAY, scalar
  if ($keys[0] eq 'definition') {
    # Possible type-checking of type specifiers?
    $struct = (ref $prev) || (ref $value); }    # previous structure, or copy from $value
  elsif (my $type = configLookup($config, 'definition', @keys)) {
    # $type can be hash for intermediate levels, else a declared type.
    $struct = (ref $type) || ($type =~ /^list\[/ ? 'ARRAY' : 'scalar');
    return if (!ref $type) && !configCheckType($type, $value, @keys); }
  else {
    configWarning("Unknown key (possible typo?)", $value, @keys);
    return; }
  if ($struct eq 'HASH') {
    $$object{$key} = {} unless $prev;           # initialize if needed
    if (!defined $value) { }
    elsif (ref $value eq 'HASH') {
      map { mergeValue($config, $$value{$_}, @keys, $_) } keys %$value; }
    else {
      configWarning("Cannot merge value into mapping", $value, @keys); } }
  elsif ($struct eq 'ARRAY') {
    $$object{$key} = [] unless $prev;           # initialize if needed
    if (defined $value) {
      push(@{ $$object{$key} }, (ref $value eq 'ARRAY' ? @$value : $value)); } }
  else {
    $$object{$key} = $value; } }

# sorta like $$config{key1}{key2}...{keyn}
sub configLookup {
  my ($config, @keys) = @_;
  my $object = $config;
  map { $object = $object && $$object{$_} } @keys;
  return $object; }

# This could evolve to return the "normalized" $value?
sub configCheckType {
  my ($type, $value, @keys) = @_;
  # pathname, boolean, int, string, phasename, classname, date
  # or list[$type], hash[$ignored], choice[lit1,lit2...]
  if    ($type eq 'boolean') { return 1 if $value =~ /^(?:true|false)$/; }
  elsif ($type eq 'int')     { return 1 if $value =~ /^(?:\+|\-)?\d+$/; }
  elsif ($type eq 'string')  { return 1; }
  elsif ($type eq 'pathname')  { return 1; }
  elsif ($type eq 'classname') { return 1 if $value =~ /^(\w+)(?:\:\:\w+)*$/; }
  elsif ($type eq 'phasename') { return 1 if grep { $value eq $_ } @PHASES; }
  elsif ($type eq 'date') { return 1 if $value =~ /^\d{4}-\d{2}-\d{2}$/; }
  elsif ($type =~ /^list\[(\w+)\]$/) {
    my $subtype = $1;
    if (((ref $value) || '') eq 'ARRAY') {
      return 1 if !grep { !configCheckType($subtype, $_, @keys) } @$value; }
    else {
      return 1 if configCheckType($subtype, $value, @keys); } }
  elsif ($type =~ /^hash\[(\w*)\]$/) {
    # No checking of internal structure of the hash ... YET!
    return 1 if ((ref $value) || '') eq 'HASH'; }
  elsif ($type =~ /^choice\[(.*)\]$/) {
    my @choices = split(/\s*,\s*/, $1);
    return 1 if grep { $value eq $_ } @choices; }
  else {
    configWarning("Unknown type $type", $value, @keys);
    return 0; }
  configWarning("Value not of type $type", $value, @keys);
  return 0; }

sub configWarning {
  my ($message, $value, @keys) = @_;
  warn "Config warning in " . ($LaTeXML::CONFIG_FILE || 'commandline options') . ": $message\n"
    . " for " . join('->', @keys) . " = $value\n"; }

#======================================================================
# Analyzing the resulting configuration to generate a Processing Plan
# Note current code doesn't support this level of abstraction,
# although it is similar to the post-processing chain.
sub makePlan {
  my ($config) = @_;
  print "Global: " . minidump($$config{global}) . "\n";
  print "Daemon: " . minidump($$config{daemon}) . "\n";
  my @processors = ();
  foreach my $phase (@PHASES) {
    my @p = sort { ($$config{$a}{position} || 0) <=> ($$config{$b}{position} || 0) }
      grep { (($$config{$_}{phase} || '') eq $phase) && (($$config{$_}{enabled} || '') eq 'true') }
      keys %$config;
    push(@processors, @p); }
  print "Processing plan:\n";
  foreach my $p (@processors) {
    print "$p : " . minidump($$config{$p}) . "\n"; }
}

# Just for debugging the plan construction...
sub minidump {
  return wrap("", "         ", minidump_aux($_[0])); }

sub minidump_aux {
  my ($value) = @_;
  my $ref = ref $value;
  if (!$ref) {
    return (defined $value ? $value : 'undef'); }
  elsif ($ref eq 'HASH') {
    return '{' . join(', ', map { $_ . '=>' . minidump($$value{$_}) } sort keys %$value) . '}'; }
  elsif ($ref eq 'ARRAY') {
    return '[' . join(', ', map { minidump($_) } @$value) . ']'; } }

#======================================================================
